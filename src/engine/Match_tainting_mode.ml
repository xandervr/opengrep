(* Iago Abal, Yoann Padioleau
 *
 * Copyright (C) 2019-2024 Semgrep Inc.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public License
 * version 2.1 as published by the Free Software Foundation, with the
 * special exception on linking described in file LICENSE.
 *
 * This library is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the file
 * LICENSE for more details.
 *)
open Common
open Fpath_.Operators
module D = Dataflow_tainting
module Var_env = Dataflow_var_env
module G = AST_generic
module H = AST_generic_helpers
module R = Rule
module PM = Core_match
module RP = Core_result
module T = Taint
module Lval_env = Taint_lval_env
module MV = Metavariable
module ME = Matching_explanation
module OutJ = Semgrep_output_v1_t
module Labels = Set.Make (String)

module LangOrd = struct
  type t = Lang.t

  let compare = Stdlib.compare
end

module LangMap = Map.Make (LangOrd)
module LangSet = Set.Make (LangOrd)
module Log = Log_tainting.Log
module Effect = Shape_and_sig.Effect
module Effects = Shape_and_sig.Effects
module Signature = Shape_and_sig.Signature

type fun_info = {
  name : IL.name;
  class_name_str : string option;
  method_properties : AST_generic.expr list;
  cfg : IL.fun_cfg;
  fdef : G.function_definition;
  is_lambda_assignment : bool;
}

(*****************************************************************************)
(* Prelude *)
(*****************************************************************************)
(* Wrapper around the tainting dataflow-based analysis. *)

(*****************************************************************************)
(* Hooks *)
(*****************************************************************************)

let hook_setup_hook_function_taint_signature = ref None

(*****************************************************************************)
(* Helpers *)
(*****************************************************************************)
module F2 = IL

module DataflowY = Dataflow_core.Make (struct
  type node = F2.node
  type edge = F2.edge
  type flow = (node, edge) CFG.t

  let short_string_of_node n = Display_IL.short_string_of_node_kind n.F2.n
end)

let get_source_requires src =
  let _pm, src_spec = T.pm_of_trace src.T.call_trace in
  src_spec.R.source_requires

(*****************************************************************************)
(* Testing whether some matches a taint spec *)
(*****************************************************************************)

let lazy_force x = Lazy.force x [@@profiling]

let unsupported_taint_analyzer_msg =
  "taint mode requires a dedicated parser; generic and regex analyzers do not \
   support taint analysis"

exception Unsupported_taint_analyzer

let unsupported_taint_analyzer_report (rule : R.taint_rule) file =
  let loc = Tok.first_loc_of_file file in
  let error =
    Core_error.mk_error ~rule_id:(fst rule.R.id)
      ~msg:unsupported_taint_analyzer_msg ~loc OutJ.SemgrepError
  in
  RP.mk_match_result []
    (Core_error.ErrorSet.singleton error)
    (Core_profiling.empty_rule_profiling (rule :> R.rule))

let normalize_match_path_to_range_file (pm : PM.t) : PM.t =
  let start_loc, _end_loc = pm.range_loc in
  let file = start_loc.pos.file in
  {
    pm with
    path = { origin = Origin.File file; internal_path_to_content = file };
  }

(*****************************************************************************)
(* Pattern match from finding *)
(*****************************************************************************)

(* If the 'requires' has the shape 'A and ...' then we assume that 'A' is the
 * preferred label for reporting the taint trace. *)
let preferred_label_of_sink ({ rule_sink; _ } : Effect.sink) =
  match rule_sink.sink_requires with
  | Some { precondition = PAnd (PLabel label :: _); _ } -> Some label
  | Some _
  | None ->
      None

let rec convert_taint_call_trace = function
  | Taint.PM (pm, _) ->
      let toks = Lazy.force pm.tokens |> List.filter Tok.is_origintok in
      Taint_trace.Toks toks
  | Taint.Call (expr, toks, ct) ->
      Taint_trace.Call
        {
          call_toks =
            AST_generic_helpers.ii_of_any (G.E expr)
            |> List.filter Tok.is_origintok;
          intermediate_vars = toks;
          call_trace = convert_taint_call_trace ct;
        }

(* For now CLI does not support multiple taint traces for a finding, and it
 * simply picks the _first_ trace from this list. So here we apply a number
 * of heuristics to make sure the first trace in this list is the most
 * relevant one. This is particularly important when using (experimental)
 * taint labels, because not all labels are equally relevant for the finding. *)
let sources_of_taints ?preferred_label taints =
  (* We only report actual sources reaching a sink. If users want Semgrep to
   * report function parameters reaching a sink without sanitization, then
   * they need to specify the parameters as taint sources. *)
  let taint_sources =
    taints
    |> List_.filter_map (fun { Effect.taint = { orig; tokens }; sink_trace } ->
           match orig with
           | Src src -> Some (src, tokens, sink_trace)
           (* even if there is any taint "variable", it's irrelevant for the
            * finding, since the precondition is satisfied. *)
           | Var _
           | Shape_var _
           | Control ->
               None)
  in
  let taint_sources =
    (* If there is a "preferred label", then sort sources to make sure this
       label is picked before others. See 'preferred_label_of_sink'. *)
    match preferred_label with
    | None -> taint_sources
    | Some label ->
        taint_sources
        |> List.stable_sort (fun (src1, _, _) (src2, _, _) ->
               match (src1.T.label = label, src2.T.label = label) with
               | true, false -> -1
               | false, true -> 1
               | false, false
               | true, true ->
                   0)
  in
  (* We prioritize taint sources without preconditions,
     selecting their traces first, and then consider sources
     with preconditions as a secondary choice. *)
  let with_req, without_req =
    taint_sources
    |> Either_.partition (fun (src, tokens, sink_trace) ->
           match get_source_requires src with
           | Some _ -> Left (src, tokens, sink_trace)
           | None -> Right (src, tokens, sink_trace))
  in
  if without_req <> [] then without_req
  else (
    Log.warn (fun m ->
        m
          "Taint source without precondition wasn't found. Displaying the \
           taint trace from the source with precondition.");
    with_req)

let trace_of_source source =
  let src, tokens, sink_trace = source in
  {
    Taint_trace.source_trace = convert_taint_call_trace src.T.call_trace;
    tokens;
    sink_trace = convert_taint_call_trace sink_trace;
  }

let pms_of_effect ~match_on (effect_ : Effect.t) =
  match effect_ with
  | ToLval _
  | CleanLval _
  | ToReturn _
  | ToSinkInCall _ ->
      []
  | ToSink
      {
        taints_with_precondition = taints, requires;
        sink = { pm = sink_pm; _ } as sink;
        merged_env;
      } -> (
      let actual_taints = List_.map (fun t -> t.Effect.taint) taints in
      let satisfies = T.taints_satisfy_requires actual_taints requires in
      if not satisfies then []
      else
        let preferred_label = preferred_label_of_sink sink in
        let taint_sources = sources_of_taints ?preferred_label taints in
        match match_on with
        | `Sink ->
            (* The old behavior used to be that, for sinks with a `requires`, we would
               generate a finding per every single taint source going in. Later deduplication
               would deal with it.
               We will instead choose to consolidate all sources into a single finding. We can
               do some postprocessing to report only relevant sources later on, but for now we
               will lazily (again) defer that computation to later.
            *)
            let traces = List_.map trace_of_source taint_sources in
            (* We always report the finding on the sink that gets tainted, the call trace
                * must be used to explain how exactly the taint gets there. At some point
                * we experimented with reporting the match on the `sink`'s function call that
                * leads to the actual sink. E.g.:
                *
                *     def f(x):
                *       sink(x)
                *
                *     def g():
                *       f(source)
                *
                * Here we tried reporting the match on `f(source)` as "the line to blame"
                * for the injection bug... but most users seem to be confused about this. They
                * already expect Semgrep (and DeepSemgrep) to report the match on `sink(x)`.
            *)
            let taint_trace = Some (lazy traces) in
            [ { sink_pm with env = merged_env; taint_trace } ]
        | `Source ->
            taint_sources
            |> List_.map (fun source ->
                   let src, tokens, sink_trace = source in
                   let src_pm, _ = T.pm_of_trace src.T.call_trace in
                   let trace =
                     {
                       Taint_trace.source_trace =
                         convert_taint_call_trace src.T.call_trace;
                       tokens;
                       sink_trace = convert_taint_call_trace sink_trace;
                     }
                   in
                   {
                     src_pm with
                     env = merged_env;
                     taint_trace = Some (lazy [ trace ]);
                   }))

(*****************************************************************************)
(* Main entry points *)
(*****************************************************************************)

let check_fundef (taint_inst : Taint_rule_inst.t) (name : IL.name) ?glob_env ?class_name
    ?signature_db ?builtin_signature_db ?call_graph fdef =
  let fdef = AST_to_IL.function_definition taint_inst.lang fdef in
  let fcfg = CFG_build.cfg_of_fdef fdef in
  let in_env, env_effects =
    Taint_input_env.mk_fun_input_env taint_inst ?glob_env fdef.fparams
  in
  let effects, mapping =
    Dataflow_tainting.fixpoint taint_inst ~in_env ~name ?class_name
      ?signature_db ?builtin_signature_db ?call_graph fcfg
  in
  let effects = Effects.union env_effects effects in
  (fcfg, effects, mapping)

let function_id_is_top_level (fn_id : Function_id.t) : bool =
  String.equal (Function_id.show fn_id) "<top_level>"

let get_arity params info lang =
  let filtered_params =
    match (lang, info.class_name_str) with
    (* Python methods: filter out 'self' and 'cls' params *)
    | Lang.Python, Some _ ->
        List.filter
          (function
            | G.Param { pname = Some (("self" | "cls"), _); _ } -> false
            | _ -> true)
          params
    (* Go methods: filter out ParamReceiver *)
    | Lang.Go, Some _ ->
        List.filter
          (function
            | G.ParamReceiver _ -> false
            | _ -> true)
          params
    | _ -> params
  in
  List.length filtered_params

(** Convert a Case pattern back into a [G.parameter list] for per-arity
    signature extraction (Clojure multi-arity / Elixir multi-clause). *)
let params_of_case_pattern (pat : G.pattern) : G.parameter list =
  let unwrap_guard (p : G.pattern) : G.pattern =
    match p with
    | G.PatWhen (inner, _guard) -> inner
    | _ -> p
  in
  let param_of_pat (p : G.pattern) : G.parameter =
    match p with
    | G.PatId (ident, id_info) ->
        G.Param
          {
            G.pname = Some ident;
            pinfo = id_info;
            ptype = None;
            pdefault = None;
            pattrs = [];
          }
    | G.PatConstructor (G.Id (("&", _amp_tok), _), [ G.PatId (ident, id_info) ])
      ->
        (* Clojure rest params: (& rest) *)
        G.ParamRest
          ( Tok.unsafe_fake_tok "&",
            {
              G.pname = Some ident;
              pinfo = id_info;
              ptype = None;
              pdefault = None;
              pattrs = [];
            } )
    | _ -> G.OtherParam (("PatUnknown", G.fake ""), [])
  in
  let inner = unwrap_guard pat in
  let pats =
    match inner with
    | G.PatList (_, pats, _)
    | G.PatTuple (_, pats, _) ->
        pats
    | _ -> [ inner ]
  in
  List_.map param_of_pat pats

(** For Clojure/Elixir functions with a single implicit param and a Switch
    body, extract per-arity cases. Returns a list of
    (params, function_body, sig_arity) sorted by decreasing arity. *)
let extract_multi_arity_cases (fdef : G.function_definition) :
    (G.parameter list * G.function_body * Shape_and_sig.sig_arity) list option =
  let params = Tok.unbracket fdef.G.fparams in
  let has_implicit =
    match params with
    | [ G.Param { G.pname = Some (name, _); _ } ] ->
        G.is_implicit_param name
    | _ -> false
  in
  if not has_implicit then None
  else
    match fdef.G.fbody with
    | G.FBStmt { G.s = G.Switch (_, _, cases); _ } ->
        let arity_cases =
          cases
          |> List.filter_map (fun (cab : G.case_and_body) ->
                 match cab with
                 | G.CasesAndBody (case_list, body) -> (
                     match case_list with
                     | [ G.Case (_, pat) ] ->
                         let case_params = params_of_case_pattern pat in
                         let rest, fixed =
                           List.partition
                             (function
                               | G.ParamRest _ -> true
                               | _ -> false)
                             case_params
                         in
                         let arity : Shape_and_sig.sig_arity =
                           match rest with
                           | _ :: _ -> Arity_at_least (List.length fixed)
                           | [] -> Arity_exact (List.length fixed)
                         in
                         Some (case_params, G.FBStmt body, arity)
                     | _ -> None)
                 | G.CaseEllipsis _ -> None)
        in
        let sorted =
          List.sort
            (fun (_, _, a1) (_, _, a2) ->
              let n1 = Shape_and_sig.int_of_sig_arity a1 in
              let n2 = Shape_and_sig.int_of_sig_arity a2 in
              Int.compare n2 n1)
            arity_cases
        in
        (match sorted with
        | [] -> None
        | _ -> Some sorted)
    | _ -> None

let check_rule per_file_formula_cache (rule : R.taint_rule) match_hook
    ?(signature_db : Shape_and_sig.signature_database option)
    ?(builtin_signature_db : Shape_and_sig.builtin_signature_database option)
    ?(shared_call_graph :
        (Call_graph.G.t * (G.name * G.name) list) option =
      None) (xconf : Match_env.xconfig) (xtarget : Xtarget.t) =
  Log.info (fun m ->
      m
        "Match_tainting_mode:\n\
         ====================\n\
         Running rule %s\n\
         ===================="
        (Rule_ID.to_string (fst rule.R.id)));
  let matches = ref [] in
  let match_on =
    (* TEMPORARY HACK to support both taint_match_on (DEPRECATED) and
     * taint_focus_on (preferred name by SR). *)
    match (xconf.config.taint_focus_on, xconf.config.taint_match_on) with
    | `Source, _
    | _, `Source ->
        `Source
    | `Sink, `Sink -> `Sink
  in
  let record_matches new_effects =
    new_effects
    |> Effects.iter (fun effect_ ->
           let effect_pms = pms_of_effect ~match_on effect_ in
           matches := List.rev_append effect_pms !matches)
  in
  let {
    path = { internal_path_to_content = file; _ };
    xlang;
    lazy_ast_and_errors;
    _;
  } : Xtarget.t =
    xtarget
  in
  try
  let lang =
    match xlang with
    | L (lang, _) -> lang
    | LSpacegrep
    | LAliengrep
    | LRegex -> raise Unsupported_taint_analyzer
  in
  let (ast, skipped_tokens), parse_time =
    Common.with_time (fun () -> lazy_force lazy_ast_and_errors)
  in
  (* TODO: 'debug_taint' should just be part of 'res'
   * (i.e., add a "debugging" field to 'Report.match_result'). *)
  match
    Match_taint_spec.taint_config_of_rule ~per_file_formula_cache xconf lang
      file (ast, []) rule
  with
  | None -> (None, None)
  | Some (taint_inst, spec_matches, expls) ->
      let glob_env, glob_effects = Taint_input_env.mk_file_env taint_inst ast in
      record_matches glob_effects;

      (* Only use signature database if cross-function taint analysis is enabled *)
      let final_signature_db, relevant_graph =
        if taint_inst.options.taint_intrafile then (
          (* Detect object initialization mappings for this file *)
          let object_mappings =
            Taint_signature_extractor.detect_object_initialization ast
              taint_inst.lang
          in
          (* Build user signature database *)
          let base_db = Builtin_models.init_signature_database signature_db in
          (* Note: object_mappings will be combined with anonymous class mappings
           * and added to the signature database after IL conversion *)

          (* Collect function metadata and prepare call graph based ordering. *)
          let add_info info (infos, info_map) =
            let infos = info :: infos in
            let info_map =
              if Shape_and_sig.FunctionMap.mem (Function_id.of_il_name info.name) info_map then info_map
              else Shape_and_sig.FunctionMap.add (Function_id.of_il_name info.name) info info_map
            in
            (infos, info_map)
          in

          let collected_infos, info_map =
            Visit_function_defs.fold_with_parent_path
              (fun (infos, info_map) opt_ent parent_path fdef ->
                match fst fdef.fkind with
                | LambdaKind
                | Arrow -> (
                    match opt_ent with
                    | None -> (infos, info_map)
                    | Some ent ->
                        match AST_to_IL.name_of_entity ent with
                        | None -> (infos, info_map)
                        | Some name ->
                            let class_name_str =
                              match parent_path with
                              | Some class_il :: _ -> Some (fst class_il.IL.ident)
                              | _ -> None
                            in
                            let fdef_il =
                              AST_to_IL.function_definition taint_inst.lang
                                fdef
                            in
                            let cfg = CFG_build.cfg_of_fdef fdef_il in
                            let info =
                              {
                                name;
                                class_name_str;
                                method_properties = [];
                                cfg;
                                fdef;
                                is_lambda_assignment = true;
                              }
                            in
                            add_info info (infos, info_map))
                | Function
                | Method
                | BlockCases -> (
                    match Option.bind opt_ent AST_to_IL.name_of_entity with
                    | None -> (infos, info_map)
                    | Some name ->
                        (* For Go methods, extract receiver type as class name *)
                        let go_receiver_name =
                          match lang with
                          | Lang.Go ->
                              Graph_from_AST.extract_go_receiver_type fdef
                          | _ -> None
                        in
                        let class_name_str =
                          match go_receiver_name with
                          | Some recv_name -> Some recv_name
                          | None -> (
                              match parent_path with
                              | Some class_il :: _ -> Some (fst class_il.IL.ident)
                              | _ -> None)
                        in
                        let method_properties =
                          match fst fdef.fkind with
                          | Method ->
                              Taint_signature_extractor.extract_method_properties
                                fdef
                          | Function
                          | LambdaKind
                          | Arrow
                          | BlockCases ->
                              []
                        in
                        let fdef_il =
                          AST_to_IL.function_definition taint_inst.lang                            fdef
                        in
                        let cfg = CFG_build.cfg_of_fdef fdef_il in
                        let info =
                          {
                            name;
                            class_name_str;
                            method_properties;
                            cfg;
                            fdef;
                            is_lambda_assignment = false;
                          }
                        in
                        add_info info (infos, info_map)))
              ([], Shape_and_sig.FunctionMap.empty)
              ast
          in
          (* Use object mappings from Object_initialization.ml *)
          let all_object_mappings = object_mappings in
          let initial_signature_db =
            Shape_and_sig.add_object_mappings base_db all_object_mappings
          in

          (* Use shared call graph if provided, otherwise compute it *)
          let call_graph =
            match shared_call_graph with
            | Some (graph, _shared_mappings) -> graph
            | None ->
                (* Compute call graph as before *)
                Graph_from_AST.build_call_graph ~lang
                  ~object_mappings:all_object_mappings ast
          in

          (* Optimize: filter call graph to only functions relevant for this rule
             Use the already-computed source/sink ranges from spec_matches *)
          let source_ranges =
            spec_matches.sources
            |> List.map (fun (rwm, _src) ->
                   let start_loc, _end_loc = rwm.Range_with_metavars.origin.range_loc in
                   (rwm.Range_with_metavars.r, start_loc.pos.file))
          in
          let sink_ranges =
            spec_matches.sinks
            |> List.map (fun (rwm, _sink) ->
                   let start_loc, _end_loc = rwm.Range_with_metavars.origin.range_loc in
                   (rwm.Range_with_metavars.r, start_loc.pos.file))
          in
          let source_functions =
            Graph_from_AST.find_functions_containing_ranges ~lang ast
              source_ranges
          in
          let sink_functions =
            Graph_from_AST.find_functions_containing_ranges ~lang ast
              sink_ranges
          in

          Log.debug (fun m ->
              m "SUBGRAPH: Found %d source functions and %d sink functions"
                (List.length source_functions)
                (List.length sink_functions));
          List.iteri
            (fun i id ->
              Log.debug (fun m ->
                  let name = Function_id.show id in
                  m "SUBGRAPH: source_function[%d] = %s" i name))
            source_functions;
          List.iteri
            (fun i id ->
              Log.debug (fun m ->
                  let name = Function_id.show id in
                  m "SUBGRAPH: sink_function[%d] = %s" i name))
            sink_functions;

          (* Write FULL call graph to dot file for debugging. Keeping for debugger *)
          (* let full_dot_file = open_out "call_graph_full.dot" in
          Call_graph.Dot.output_graph full_dot_file call_graph;
          close_out full_dot_file;
          Log.debug (fun m -> m "FULL GRAPH: Wrote full call graph to call_graph_full.dot"); *)
          let relevant_graph =
            Graph_reachability.compute_relevant_subgraph call_graph
              ~sources:source_functions ~sinks:sink_functions
          in
          let relevant_graph =
            if List.exists function_id_is_top_level source_functions then (
              Log.debug (fun m ->
                  m
                    "SUBGRAPH: Top-level source found, keeping all %d \
                     discovered functions in analysis"
                    (List.length collected_infos));
              List.iter
                (fun info ->
                  Call_graph.G.add_vertex relevant_graph
                    (Function_id.of_il_name info.name))
                collected_infos;
              relevant_graph)
            else relevant_graph
          in
          let relevant_graph =
            (* The call graph intentionally stays conservative, and some
               language constructs such as JavaScript object-literal methods can
               still be resolved later from taint shapes. Keep explicit source
               and sink containers in the analysis graph so the dataflow pass
               gets a chance to use those shapes instead of pruning the sink
               before analysis starts. *)
            List.iter (Call_graph.G.add_vertex relevant_graph) source_functions;
            List.iter (Call_graph.G.add_vertex relevant_graph) sink_functions;
            relevant_graph
          in

          (* Write call graph to dot file for debugging *)
          (* let dot_file = open_out "call_graph.dot" in
          Call_graph.Dot.output_graph dot_file relevant_graph;
          close_out dot_file;
          Log.debug (fun m -> m "SUBGRAPH: Wrote call graph to call_graph.dot"); *)
          let analysis_order =
            Call_graph.Topo.fold
              (fun fn acc -> fn :: acc)
              relevant_graph []
            |> List.rev
          in
          Log.debug (fun m ->
              m "TAINT_TOPO: Analysis order has %d functions"
                (List.length analysis_order));
          List.iteri
            (fun i node ->
              Log.debug (fun m ->
                  m "TAINT_TOPO: [%d] %s" i (Function_id.show node)))
            analysis_order;

          let run_check_fundef_if_needed (info : fun_info)
              (updated_db : Shape_and_sig.signature_database) :
              Shape_and_sig.signature_database =
            let _flow, fdef_effects, _mapping =
              check_fundef taint_inst info.name ~glob_env
                ?class_name:info.class_name_str ~signature_db:updated_db
                ?builtin_signature_db
                ?call_graph:(Some relevant_graph) info.fdef
            in
            (* For lambda assignments we only record "unconditional" ToSink
               effects — those where the taint at the sink comes from a
               concrete pattern-source match (e.g. a parameter declared as a
               source via `pattern-inside: function $X(..., $RES, ...) {...}`).
               Effects whose taint is purely parameterized (BArg) still ride
               through the signature at resolved call sites; effects mixing
               both get an Src-only slice surfaced here. *)
            let keep_src_toSink_only (eff : Effect.t) : Effect.t option =
              match eff with
              | Effect.ToSink si ->
                  let items, precond = si.taints_with_precondition in
                  let src_items =
                    List.filter
                      (fun (i : Effect.taint_to_sink_item) ->
                        match i.taint.orig with
                        | Taint.Src _ -> true
                        | _ -> false)
                      items
                  in
                  if List_.null src_items then None
                  else
                    Some
                      (Effect.ToSink
                         {
                           si with
                           taints_with_precondition = (src_items, precond);
                         })
              | _ -> None
            in
            let effects_to_record =
              if info.is_lambda_assignment then
                Effects.filter_map keep_src_toSink_only fdef_effects
              else fdef_effects
            in
            record_matches effects_to_record;
            updated_db
          in

          let process_fun_info info db =
            match extract_multi_arity_cases info.fdef with
            | Some arity_cases ->
                (* Multi-arity function: extract one signature per arity branch *)
                List.fold_left
                  (fun acc_db (case_params, case_body, arity) ->
                    let synthetic_fdef : G.function_definition =
                      {
                        G.fparams = Tok.unsafe_fake_bracket case_params;
                        frettype = None;
                        fkind = info.fdef.G.fkind;
                        fbody = case_body;
                      }
                    in
                    let fdef_il =
                      AST_to_IL.function_definition lang synthetic_fdef
                    in
                    let cfg = CFG_build.cfg_of_fdef fdef_il in
                    let db', _sig =
                      Taint_signature_extractor
                      .extract_signature_with_file_context ~arity ~db:acc_db
                        ?builtin_signature_db taint_inst ~name:info.name
                        ~method_properties:info.method_properties
                        ~call_graph:(Some relevant_graph) cfg ast
                    in
                    db')
                  db arity_cases
            | None ->
                (* Single-arity path (unchanged logic) *)
                let params = Tok.unbracket info.fdef.fparams in
                let arity = get_arity params info lang in
                let updated_db, _signature =
                  Taint_signature_extractor.extract_signature_with_file_context
                    ~arity:(Shape_and_sig.Arity_exact arity) ~db
                    ?builtin_signature_db taint_inst ~name:info.name
                    ~method_properties:info.method_properties
                    ~call_graph:(Some relevant_graph) info.cfg ast
                in
                (* For Kotlin, if the last parameter is a lambda (function type),
                 * also extract signature with arity-1 to handle trailing lambda syntax:
                 * f(a, b) vs f(a) { b } *)
                let updated_db =
                  if Lang.equal lang Lang.Kotlin && arity >= 1 then
                    let last_param_is_lambda =
                      match List.rev params with
                      | G.Param { G.ptype = Some { t = G.TyFun _; _ }; _ } :: _
                        ->
                          true
                      | _ -> false
                    in
                    if last_param_is_lambda then
                      let db', _ =
                        Taint_signature_extractor
                        .extract_signature_with_file_context
                          ~arity:(Shape_and_sig.Arity_exact (arity - 1))
                          ~db:updated_db ?builtin_signature_db taint_inst
                          ~name:info.name
                          ~method_properties:info.method_properties
                          ~call_graph:(Some relevant_graph) info.cfg ast
                      in
                      db'
                    else updated_db
                  else updated_db
                in
                updated_db
          in

          let signature_db_after_order =
            List.fold_left
              (fun db node ->
                Log.debug (fun m ->
                    m "TAINT_SIGBUILD: Processing %s" (Function_id.show node));
                match Shape_and_sig.FunctionMap.find_opt node info_map with
                | None ->
                    Log.debug (fun m ->
                        m "TAINT_SIGBUILD: fn_id NOT FOUND in info_map!");
                    db
                | Some info ->
                    Log.debug (fun m ->
                        m
                          "TAINT_SIGBUILD: fn_id found in info_map, \
                           processing...");
                    let new_db = process_fun_info info db in
                    Log.debug (fun m ->
                        m
                          "TAINT_SIGBUILD: After processing, db.signatures \
                           size=%d"
                          (Shape_and_sig.FunctionMap.cardinal
                             new_db.Shape_and_sig.signatures));
                    new_db)
              initial_signature_db analysis_order
          in

          let final_signature_db = signature_db_after_order in
          List.iter
            (fun node ->
              match Shape_and_sig.FunctionMap.find_opt node info_map with
              | None -> ()
              | Some info ->
                  ignore (run_check_fundef_if_needed info final_signature_db))
            analysis_order;
          (* Skip the "remaining functions" phase entirely - if a function isn't
             in the relevant subgraph, we don't need to analyze it *)
          (Some final_signature_db, Some relevant_graph))
        else (
          (* Cross-function taint analysis disabled: use main branch behavior *)
          Visit_function_defs.visit
            (fun opt_ent fdef ->
              match fst fdef.fkind with
              | LambdaKind
              | Arrow ->
                  (* We do not need to analyze lambdas here, they will be analyzed
               together with their enclosing function. This would just duplicate
               work. *)
                  ()
              | Function
              | Method
              | BlockCases ->
                  let opt_name =
                    let* ent = opt_ent in
                    AST_to_IL.name_of_entity ent
                  in
                  match opt_name with
                  | None -> ()
                  | Some name ->
                      Log.info (fun m ->
                          m
                            "Match_tainting_mode:\n\
                             --------------------\n\
                             Checking func def: %s\n\
                             --------------------"
                            (IL.str_of_name name));
                      let _flow, fdef_effects, _mapping =
                        check_fundef taint_inst name ~glob_env
                          ?builtin_signature_db fdef
                      in
                      record_matches fdef_effects)
            ast;
          (None, None))
      in

      (* Check execution of statements during object initialization. *)
      Visit_class_defs.visit
        (fun opt_ent cdef ->
          let opt_name =
            let* ent = opt_ent in
            AST_to_IL.name_of_entity ent
          in
          let fields =
            cdef.G.cbody |> Tok.unbracket
            |> List_.map (function G.F x -> x)
            |> G.stmt1
          in
          let stmts = AST_to_IL.stmt taint_inst.lang fields in
          let cfg, lambdas = CFG_build.cfg_of_stmts stmts in
          let init_effects, _mapping =
            Dataflow_tainting.fixpoint taint_inst ?name:opt_name
              ?signature_db:final_signature_db ?builtin_signature_db
              ?call_graph:relevant_graph
              IL.{ params = []; cfg; lambdas }
          in
          record_matches init_effects)
        ast;

      (* Check the top-level statements.
       * In scripting languages it is not unusual to write code outside
       * function declarations and we want to check this too. We simply
       * treat the program itself as an anonymous function. *)
      let (), match_time =
        Common.with_time (fun () ->
            let xs = AST_to_IL.stmt taint_inst.lang (G.stmt1 ast) in
            let cfg, lambdas = CFG_build.cfg_of_stmts xs in
            let top_level_name =
              let fake_tok = Tok.unsafe_fake_tok "<top_level>" in
              IL.{ ident = ("<top_level>", fake_tok); sid = G.SId.unsafe_default; id_info = G.empty_id_info () }
            in
            let top_effects, _mapping =
              Dataflow_tainting.fixpoint taint_inst ~name:top_level_name
                ?signature_db:final_signature_db ?builtin_signature_db
                ?call_graph:relevant_graph
                IL.{ params = []; cfg; lambdas }
            in
            record_matches top_effects)
      in
      let matches =
        !matches
        |> List_.map normalize_match_path_to_range_file
        (* same post-processing as for search-mode in Match_rules.ml *)
        |> PM.uniq
        |> PM.no_submatches (* see "Taint-tracking via ranges" *) |> match_hook
      in

      let errors = Parse_target.errors_from_skipped_tokens skipped_tokens in
      let report =
        RP.mk_match_result matches errors
          {
            Core_profiling.rule_id = fst rule.R.id;
            rule_parse_time = parse_time;
            rule_match_time = match_time;
          }
      in
      let explanations =
        if xconf.matching_explanations then
          [
            {
              ME.op = OutJ.Taint;
              children = expls;
              matches = report.matches;
              pos = snd rule.id;
              extra = None;
            };
          ]
        else []
      in
      let report = { report with explanations } in
      (Some report, final_signature_db)
  with
  | Unsupported_taint_analyzer ->
      (Some (unsupported_taint_analyzer_report rule file), None)

let check_rules ~match_hook
    ~(per_rule_boilerplate_fn :
       R.rule ->
       (unit -> Core_profiling.rule_profiling Core_result.match_result option) ->
       Core_profiling.rule_profiling Core_result.match_result option)
    (rules : R.taint_rule list) (xconf : Match_env.xconfig)
    (xtarget : Xtarget.t) :
    Core_profiling.rule_profiling Core_result.match_result list =
  (* Check for language support warnings when taint_intrafile is enabled *)
  (Dataflow_tainting.reset_constructor ();
   match rules with
   | rule :: _ -> (
       (* Check if any rule has taint_intrafile enabled *)
       let has_taint_intrafile =
         match rule.options with
         | Some opts -> opts.taint_intrafile
         | None -> xconf.config.taint_intrafile
       in
       if has_taint_intrafile then
         match Xlang.to_lang xtarget.xlang with
         | Ok _ ->
             ()
         | Error _ ->
             Logs.warn (fun m ->
                 m
                   "Cross-function taint analysis (--taint-intrafile) is only \
                    available for languages with a dedicated parser. Generic \
                    and regex analyzers do not support taint mode."))
   | [] -> ());

  (* We create a "formula cache" here, before dealing with individual rules, to
     permit sharing of matches for sources, sanitizers, propagators, and sinks
     between rules.

     In particular, this expects to see big gains due to shared propagators,
     in Semgrep Pro. There may be some benefit in OSS, but it's low-probability.
  *)
  let per_file_formula_cache =
    Formula_cache.mk_specialized_formula_cache rules
  in

  (* Collect all languages that have rules with taint_intrafile enabled *)
  let langs_needing_call_graph =
    rules
    |> List.fold_left
         (fun acc rule ->
           let xconf_rule =
             Match_env.adjust_xconfig_with_rule_options xconf rule.R.options
           in
           if xconf_rule.config.taint_intrafile then
             match Xlang.to_lang rule.R.target_analyzer with
             | Ok lang -> LangSet.add lang acc
             | Error _ -> acc
           else acc)
         LangSet.empty
  in

  (* Pre-compute call graph and builtin db for each language that needs it.
     The call graph depends on the AST structure and language, so we compute
     it once per language and share across rules that need it. *)
  let call_graph_by_lang =
    LangSet.fold
      (fun lang acc ->
        let ast, _skipped_tokens = lazy_force xtarget.lazy_ast_and_errors in
        let object_mappings =
          Taint_signature_extractor.detect_object_initialization ast lang
        in
        let call_graph =
          Graph_from_AST.build_call_graph ~lang ~object_mappings ast
        in
        LangMap.add lang (call_graph, object_mappings) acc)
      langs_needing_call_graph LangMap.empty
  in

  let builtin_db_by_lang =
    LangSet.fold
      (fun lang acc ->
        let builtin_db = Builtin_models.create_all_builtin_models lang in
        LangMap.add lang builtin_db acc)
      langs_needing_call_graph LangMap.empty
  in

  let results =
    rules
    |> List.filter_map (fun rule ->
           let xconf =
             Match_env.adjust_xconfig_with_rule_options xconf rule.R.options
           in
           (* Only pass call graph and builtin db if taint_intrafile is enabled for this rule *)
           let rule_shared_call_graph, rule_builtin_signature_db =
             if xconf.config.taint_intrafile then
               match Xlang.to_lang rule.R.target_analyzer with
               | Ok lang ->
                   ( LangMap.find_opt lang call_graph_by_lang,
                     LangMap.find_opt lang builtin_db_by_lang )
               | Error _ -> (None, None)
             else (None, None)
           in
           per_rule_boilerplate_fn
             (rule :> R.rule)
             (fun () ->
               Logs_.with_debug_trace ~__FUNCTION__
                 ~pp_input:(fun _ ->
                   "target: "
                   ^ !!(xtarget.path.internal_path_to_content)
                   ^ "\nruleid: "
                   ^ (rule.id |> fst |> Rule_ID.to_string))
                 (fun () ->
                   let report, _signature_db =
                     check_rule per_file_formula_cache rule match_hook
                       ?builtin_signature_db:rule_builtin_signature_db
                       ~shared_call_graph:rule_shared_call_graph xconf xtarget
                   in
                   report)))
  in

  results
